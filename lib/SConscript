Import('variant')

import os
import subprocess
import atexit

incs = []
objs = []
rttestmainobj = ""
testsrcs = []

subdirs = [os.path.join("plf", variant['target']),
           "common", "net", "skal"]

for subdir in subdirs:
    artefacts = SConscript(os.path.join(subdir, "SConscript"),
            exports={'variant': variant})
    variant['env'].Append(CPPPATH = [os.path.join("#lib", subdir, "include")])
    if subdir == "skal":
        variant['env'].Append(CPPPATH = [os.path.join("#lib", subdir, "src")])

    for key, value in artefacts.items():
        if key == 'incs':
            incs.extend([os.path.join("lib", subdir, inc) for inc in value])

        elif key == 'objs':
            objs.extend(value)

        elif key == 'rttestmainobj':
            rttestmainobj = value

        elif key == 'testsrcs':
            for src in value:
                testsrcs.append(os.path.join(subdir, src))

variant['env'].StaticLibrary('skal', objs)

# NB: We have to do a bit of gymnastics to get scons to install files
# or directories that are not the results of some build process. This
# is because scons will use the relative paths from the variant
# directory, not the source directory. The way to solve that is to use
# absolute paths for any file/directory in the source directories.
# NB2: Don't use `os.getcwd()` here, it will give you the variant
# directory.
for inc in incs:
    variant['env'].Install(variant['build_inc'], "#" + inc)

# Build doxygen documentation
# NB: I can't get `Command()` to work, so I just do it manually when scons
# finishes...
if variant['env'].GetOption('make_doc') and variant['env']['HAS_DOXYGEN'] == "yes":
    cmd = [os.path.join(variant['topdir'], "mkdoc.py")]
    if variant['env']['HAS_DOT'] == "yes":
        cmd.append("--hasdot")
    cmd.append(variant['build_doc'])
    for inc in incs:
        cmd.append(inc)
    atexit.register(subprocess.call, cmd)

# Build test program
if len(testsrcs) > 0:
    testobjs = [variant['env'].StaticObject(src) for src in testsrcs]
    testobjs.append(rttestmainobj)
    variant['env'].Program("skal_unit_tests", testobjs,
            LIBS=['skal', 'cds', 'rttest', 'rtsys'])
